buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'com.sun.mail:javax.mail:1.6.2'
    }
}

import java.util.Properties
import javax.mail.*
import javax.mail.internet.InternetAddress
import javax.mail.internet.MimeMessage


plugins {
    id 'java'
    id 'jacoco'
    id "com.github.spacialcircumstances.gradle-cucumber-reporting" version "0.1.25"
    id 'org.sonarqube' version '4.4.1.3373'
    id 'maven-publish'

}

group = 'org.example'
version = '1.0-SNAPSHOT'

repositories {
    mavenCentral()
}
cucumberReports {
    outputDir = file('reports/report.html')
    buildId = '0'
    reports = files('reports/example-report.json')
}

dependencies {
    testImplementation 'io.cucumber:cucumber-java:6.0.0'
    testImplementation 'io.cucumber:cucumber-junit:6.0.0'
    testImplementation 'junit:junit:4.12'
    implementation 'com.sun.mail:javax.mail:1.6.2'

}

jacoco {
    toolVersion = "0.8.8"
}

javadoc {
    options.encoding = 'UTF-8'
    options.charSet = 'UTF-8'
    destinationDir = file("${layout.buildDirectory.get()}/docs/javadoc")


    options.author = true
    options.version = true
    options.windowTitle = "TP5 - Documentation API"
    options.docTitle = "TP5 - Documentation de l'API"
}

jacocoTestReport {
    dependsOn test

    reports {
        xml.required = true
        html.required = true
        html.outputLocation = layout.buildDirectory.dir("reports/jacoco")
    }
}

publishing {
    repositories {
        maven {
            url = findProperty("url_maven")
            credentials {
                username = findProperty("mavenRepoUser")
                password = findProperty("mavenRepoPassword")
            }
        }
    }

    publications {
        maven(MavenPublication) {
            groupId 'attia.oussama.abderraouf'
            from components.java
        }
    }
}

//
// tasks.named('sonar').configure {
//     dependsOn test, jacocoTestReport
// }

task notifySlack {
    doLast {
        def webhookUrl = project.findProperty('slackWebhook') ?: findProperty("SLACK_WEBHOOK")

        if (webhookUrl) {
            def message = """{
                "text": "Déploiement réussi !",
                "attachments": [{
                    "color": "good",
                    "fields": [
                        {"title": "Projet", "value": "${project.name}", "short": true},
                        {"title": "Version", "value": "${project.version}", "short": true},
                        {"title": "Groupe", "value": "${project.group}", "short": true},
                        {"title": "Date", "value": "${new Date()}", "short": false}
                    ]
                }]
            }"""

            def connection = new URL(webhookUrl).openConnection()
            connection.setRequestMethod("POST")
            connection.setDoOutput(true)
            connection.setRequestProperty("Content-Type", "application/json")

            connection.outputStream.withWriter { writer ->
                writer.write(message)
            }

            def responseCode = connection.responseCode
            if (responseCode == 200) {
                println "Notification Slack envoyée avec succès"
            } else {
                println "Erreur lors de l'envoi de la notification Slack: ${responseCode}"
            }
        } else {
            println "Webhook Slack non configuré"
        }
    }
}

task notifyEmail {
    doLast {
        def emailTo = project.findProperty('emailTo') ?: findProperty("EMAIL_TO")
        def emailFrom = project.findProperty('emailFrom') ?: findProperty("EMAIL_FROM")
        def emailPassword = project.findProperty('emailPassword') ?: findProperty("EMAIL_PASSWORD")

        if (!emailTo || !emailFrom || !emailPassword) {
            println "Configuration email incomplete"
            return
        }

        def props = new Properties()
        props.put("mail.smtp.auth", "true")
        props.put("mail.smtp.starttls.enable", "true")
        props.put("mail.smtp.starttls.required", "true")
        props.put("mail.smtp.host", "smtp.gmail.com")
        props.put("mail.smtp.port", "587")
        props.put("mail.smtp.ssl.protocols", "TLSv1.2")
        props.put("mail.smtp.ssl.trust", "smtp.gmail.com")
        props.put("mail.debug", "false")

        try {
            def session = Session.getInstance(props,
                    new Authenticator() {
                        protected PasswordAuthentication getPasswordAuthentication() {
                            return new PasswordAuthentication(emailFrom, emailPassword)
                        }
                    })

            def message = new MimeMessage(session)
            message.setFrom(new InternetAddress(emailFrom))
            message.setRecipients(Message.RecipientType.TO,
                    InternetAddress.parse(emailTo))
            message.setSubject("Deploiement reussi - ${project.name}")

            def textContent = """
Deploiement reussi !

Projet: ${project.name}
Version: ${project.version}
Groupe: ${project.group}
Date: ${new Date()}

La librairie a ete deployee avec succes.
            """

            message.setText(textContent)

            Transport.send(message)
            println "Email envoye avec succes a ${emailTo}"

        } catch (Exception e) {
            println "Erreur email: ${e.message}"
            e.printStackTrace()
        }
    }
}
// Lier la notification au déploiement
publish.finalizedBy notifySlack
notifySlack.finalizedBy notifyEmail